plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

def Properties properties = new Properties()
properties.load(project.rootProject.file('config.properties').newDataInputStream())

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.untilled.roadcapture"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField("String", "API_URL_BASE", properties.getProperty("api.url.base"))
        buildConfigField("String", "API_EXT_TMAP_KEY", properties.getProperty("api.ext.tmap.key"))

        buildConfigField("String", "SOCIAL_NAVER_CLIENT_ID", properties.getProperty("social.naver.client-id"))
        buildConfigField("String", "SOCIAL_NAVER_CLIENT_SECRET", properties.getProperty("social.naver.client-secret"))
        buildConfigField("String", "SOCIAL_NAVER_CLIENT_NAME", properties.getProperty("social.naver.client-name"))
        buildConfigField("String", "SOCIAL_KAKAO_CLIENT_ID", properties.getProperty("social.kakao.client-id"))
        buildConfigField("String", "SOCIAL_GOOGLE_CLIENT_ID", properties.getProperty("social.google.client-id"))
        buildConfigField("String", "SOCIAL_FACEBOOK_CLIENT_ID", properties.getProperty("social.facebook.client-id"))
        buildConfigField("String", "SOCIAL_FACEBOOK_SECRET_ID", properties.getProperty("social.facebook.secret-id"))
    }

    kapt {
        javacOptions {
            // These options are normally set automatically via the Hilt Gradle plugin, but we
            // set them manually to workaround a bug in the Kotlin 1.5.20
            option("-Adagger.fastInit=ENABLED")
            option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

kapt {
    correctErrorTypes = true
}

def logger_version = '2.2.0'
def retrofit_version = '2.9.0'
def fragment_version = '1.2.0-rc04'
def coroutine_version =  '1.5.2'
def glide_version = '4.12.0'
def camerax_version = '1.0.1'
def circle_imageview_version = '3.1.0'
def read_more_textview_version = '1.0.0'
def lottie_version = "4.1.0"
def okhttp_version = "4.6.0"
def room_version = "2.3.0"
def dexter_version = "6.2.3"
def paging_version = "3.0.0"
def rxjava_version = "3.1.3"
def rx_version = "3.0.0"
def naverlogin_version = "4.2.6"
def kakaologin_version = "2.8.4"
def facebooklogin_version = "[8,9)"
def google_play_services_version = "20.0.1"
def work_version = "2.6.0"
def saripaar_version = "2.0.3"
def desugar_version = "1.1.5"

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////

    /**
     * coroutine
     */
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:$coroutine_version"

    /**
     * okhttp3
     */
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    /**
     * rxjava
     */
    implementation "io.reactivex.rxjava3:rxandroid:${rx_version}"
    implementation "io.reactivex.rxjava3:rxjava:${rxjava_version}"
    implementation "io.reactivex.rxjava3:rxkotlin:${rx_version}"

    /**
     * retrofit2 + rxjava3
     */
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    /**
     * desugar
     */
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${desugar_version}"

    /**
     * window manager
     */
    implementation 'androidx.window:window:1.0.0-alpha09'

    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////

    /**
     * jetpack - paging3
     */
    implementation "androidx.paging:paging-runtime:$paging_version"
    implementation "androidx.paging:paging-rxjava3:$paging_version"

    /**
     * jetpack - room
     */
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    /**
     * jetpack - dagger + hilt
     */
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    /**
     * jetpack - fragment
     */
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.fragment:fragment-testing:$fragment_version"

    /**
     * jetpack - navigation
     */
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    /**
     * jetpack - camera x
     */
    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation 'androidx.camera:camera-view:1.0.0-alpha27'

    /**
     * jetpack - work manager
     */
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-rxjava2:$work_version"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    implementation "androidx.work:work-gcm:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"
    implementation "androidx.work:work-multiprocess:$work_version"

    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////

    /**
     * social - naver map
     */
    implementation 'com.naver.maps:map-sdk:3.13.0'

    /**
     * social - google play service auth
     */
    implementation "com.google.android.gms:play-services-auth:${google_play_services_version}"

    /**
     * social - google play service location
     */
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    /**
     * social - login
     */
    implementation "com.naver.nid:naveridlogin-android-sdk:${naverlogin_version}"
    implementation "com.kakao.sdk:v2-user:${kakaologin_version}"
    implementation "com.facebook.android:facebook-login:${facebooklogin_version}"

    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////

    /**
     * util - logger
     */
    implementation "com.orhanobut:logger:$logger_version"

    /**
     * util - dexter permission
     */
    implementation "com.karumi:dexter:$dexter_version"

    /**
     * util - ucrop
     */
    implementation 'com.github.yalantis:ucrop:2.2.6-native'

    /**
     * util - glide
     */
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    /**
     * util - read more textview
     */
    implementation "kr.co.prnd:readmore-textview:$read_more_textview_version"

    /**
     * util - circle image view
     */
    implementation "de.hdodenhof:circleimageview:$circle_imageview_version"

    /**
     * util - lottie animation
     */
    implementation "com.airbnb.android:lottie:$lottie_version"

    /**
     * util - saripaar validation
     */
    implementation "com.mobsandgeeks:android-saripaar:${saripaar_version}"

    /**
     * util - kotpref(sharedpref)
     */
    implementation 'com.chibatching.kotpref:kotpref:2.13.1'

    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////

    /**
     * test
     */
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

secrets {
    // Change the properties file from the default "local.properties" in your root project
    // to another properties file in your root project.
    propertiesFileName 'config.properties'
}